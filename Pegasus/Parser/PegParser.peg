@namespace Pegasus.Parser
@classname PegParser
@using System.Globalization
@using System.Linq
@using System.Text.RegularExpressions
@using Pegasus.Expressions
@using Pegasus.Properties

grammar <Grammar>
  = __ settings:setting* rules:rule* EOF {
        new Grammar(rules, settings, rulesEnd)
    }

setting <KeyValuePair<Identifier, object>>
  = key:settingName value:(<object> string / code / type) semicolon? {
        new KeyValuePair<Identifier, object>(key, value)
    }

settingName <Identifier> -lexical
  = "@" name:identifier { name }

rulePreamble
  = (!quantifier expressionType)? ruleFlag* equals

rule <Rule>
  = name:identifier type:expressionType? flags:ruleFlag* equals expression:expression semicolon? {{
        var typeValue = type.SingleOrDefault();
        return new Rule(
            identifier: name,
            expression: typeValue != null ? new TypedExpression(typeValue, expression) : expression,
            flags: flags);
    }}

ruleFlag <Identifier> -memoize
  = "-" flag:identifier { flag }

expression <Expression>
  = choice:choice

expressionType <CodeSpan> -memoize
  = lt name:type gt { name }
  / lt #ERROR{ "PEG0014:" + Resources.PEG0014_INVALID_TYPE }

choice <Expression>
  = choices:sequence<1,,slash> { choices.Count == 1 ? choices[0] : new ChoiceExpression(choices) }

sequence <Expression>
  = elements:labeled* code:action {
        new SequenceExpression(elements.Concat(new Expression[] { code }))
    }
  / elements:labeled* {
        elements.Count == 1
            ? elements[0]
            : new SequenceExpression(elements)
    }

labeled <Expression> -memoize
  = label:identifier colon expression:prefixed {
        new PrefixedExpression(label, expression)
    }
  / prefixed
  / &"#STATE"i code:action { code }

prefixed <Expression>
  = and code:code {
        new AndCodeExpression(code)
    }
  / and expression:suffixed {
        new AndExpression(expression)
    }
  / not code:code {
        new NotCodeExpression(code)
    }
  / not expression:suffixed {
        new NotExpression(expression)
    }
  / suffixed

suffixed <Expression>
  = expression:primary q:quantifier {
        new RepetitionExpression(expression, q)
    }
  / primary

primary <Expression> -memoize
  = name:identifier !(rulePreamble) { new NameExpression(name) }
  / literal
  / class
  / dot { new WildcardExpression() }
  / lparen type:expressionType? expression:expression rparen {{
        var typeValue = type.SingleOrDefault();
        return typeValue != null ? new TypedExpression(typeValue, expression) : expression;
    }}

quantifier <Quantifier> -memoize
  = q:question { new Quantifier(qStart, qEnd, min: 0, max: 1) }
  / q:star { new Quantifier(qStart, qEnd, min: 0, max: null) }
  / q:plus { new Quantifier(qStart, qEnd, min: 1, max: null) }
  / start:lt min:integer "," max:integer? "," delimiter:expression? end:gt { new Quantifier(startStart, endEnd, min: min, max: max.Cast<int?>().SingleOrDefault(), delimiter: delimiter.SingleOrDefault()) }
  / start:lt min:integer "," max:integer? end:gt { new Quantifier(startStart, endEnd, min: min, max: max.Cast<int?>().SingleOrDefault()) }
  / start:lt count:integer end:gt { new Quantifier(startStart, endEnd, min: count, max: count) }

integer <int>
  = digits:digit+ { int.Parse(string.Concat(digits)) }

action <Expression>
  = type:actionType code:code { new CodeExpression(code, type) }

actionType <CodeType>
  = "#ERROR"i { CodeType.Error }
  / "#STATE"i { CodeType.State }
  /           { CodeType.Result }

code <CodeSpan>
  = "{" contents:braceContents "}" __ { new CodeSpan(contents, contentsStart, contentsEnd) }
  / "{" #ERROR{ "PEG0011:" + Resources.PEG0011_UNTERMINATED_CODE }

braceContents
  = parts:(nonBraceCharacters / "{" braceContents "}")* { string.Concat(parts) }

nonBraceCharacters
  = chars:nonBraceCharacter+ { string.Concat(chars) }

nonBraceCharacter
  = [^{}]

equals    = "=" __ { "=" }
colon     = ":" __ { ":" }
semicolon = ";" __ { ";" }
slash     = "/" __ { "/" }
and       = "&" __ { "&" }
not       = "!" __ { "!" }
question  = "?" __ { "?" }
star      = "*" __ { "*" }
plus      = "+" __ { "+" }
lparen    = "(" __ { "(" }
rparen    = ")" __ { ")" }
dot       = "." __ { "." }
lt        = "<" __ { "." }
gt        = ">" __ { "." }
comma     = "," __ { "," }
lbracket  = "[" __ { "[" }
rbracket  = "]" __ { "]" }

identifier <Identifier> -memoize -lexical
  = name:(!digit (letter / digit / "_" / "$")+) __ { new Identifier(name, nameStart, nameEnd) }

type <CodeSpan>
  = type:(nonArrayType rankSpecifiers*) { new CodeSpan(type, typeStart, typeEnd, value: Regex.Replace(Regex.Replace(type, @"(?<!,)\s+|\s+(?=[,\]])", ""), @",(?=\w)", ", ")) }

nonArrayType
  = nullableType
  / typeName

nullableType
  = typeName question

rankSpecifiers
  = lbracket dimSeparator? rbracket

dimSeparator
  = comma+

typeName
  = identifier typeArgumentList? dot typeName
  / identifier typeArgumentList?

typeArgumentList
  = lt typeArguments gt
   
typeArguments
  = type comma typeArguments
  / type ""

literal <Expression> -lexical
  = value:(doubleQuotedString / singleQuotedString) flags:("i" / "r" / "ir" / "ri" / ) __ {
        new LiteralExpression(valueStart, flagsEnd, value, ignoreCase: flags.Contains('i'), fromResource: flags.Contains('r'))
    }

string -lexical
  = string:(doubleQuotedString / singleQuotedString) __ { @string }

doubleQuotedString
  = '"' chars:doubleQuotedCharacter* '"' { string.Concat(chars) }
  / '"' #ERROR{ "PEG0009:" + Resources.PEG0009_UNTERMINATED_STRING }

doubleQuotedCharacter
  = simpleDoubleQuotedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleDoubleQuotedCharacter
  = !('"' / "\\" / eolChar) char:. { @char }

singleQuotedString
  = "'" chars:singleQuotedCharacter* "'" { string.Concat(chars) }
  / "'" #ERROR{ "PEG0009:" + Resources.PEG0009_UNTERMINATED_STRING }

singleQuotedCharacter
  = simpleSingleQuotedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleSingleQuotedCharacter
  = !("'" / "\\" / eolChar) char:. { @char }

class <Expression> -lexical
  = "[" inverted:"^"? parts:(classCharacterRange / classCharacter)* ("]" / #ERROR{ "PEG0010:" + Resources.PEG0010_UNTERMINATED_CLASS }) flags:"i"? __ {
        new ClassExpression(
            parts,
            negated: inverted.SingleOrDefault() == "^",
            ignoreCase: flags.SingleOrDefault() == "i")
    }

classCharacterRange <CharacterRange>
  = begin:bracketDelimitedCharacter "-" end:bracketDelimitedCharacter {
        new CharacterRange(begin[0], end[0])
    }

classCharacter <CharacterRange>
  = char_:bracketDelimitedCharacter {
        new CharacterRange(char_[0], char_[0])
    }

bracketDelimitedCharacter
  = simpleBracketDelimitedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleBracketDelimitedCharacter
  = !("]" / "\\" / eolChar) char:. { @char }

simpleEscapeSequence
  = "\\" !(digit / "x" / "u" / eolChar) char:. {
        @char
            .Replace("b", "\b")
            .Replace("f", "\f")
            .Replace("n", "\n")
            .Replace("r", "\r")
            .Replace("t", "\t")
            .Replace("v", "\v")
    }

zeroEscapeSequence
  = "\\0" !digit { "\0" }

hexEscapeSequence
  = "\\x" digits:(hexDigit hexDigit) {
        ((char)Convert.ToInt32(digits, 16)).ToString()
    }

unicodeEscapeSequence
  = "\\u" digits:(hexDigit hexDigit hexDigit hexDigit) {
        ((char)Convert.ToInt32(digits, 16)).ToString()
    }

eolEscapeSequence
  = "\\" eol:eol { eol }

digit
  = [0-9]

hexDigit
  = [0-9a-fA-F]

letter
  = lowerCaseLetter
  / upperCaseLetter

lowerCaseLetter
  = [a-z]

upperCaseLetter
  = [A-Z]

__ = (whitespace / eol / comment)*

comment
  = singleLineComment
  / multiLineComment

singleLineComment
  = "//" (!eolChar .)*

multiLineComment
  = "/*" (!"*/" .)* "*/"

eol
  = "\n"
  / "\r\n"
  / "\r"
  / "\u2028"
  / "\u2029"

eolChar
  = [\n\r\u2028\u2029]

whitespace
  = [ \t\v\f\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]

EOF
  = !.
  / unexpected:. #ERROR{ "PEG0008:" + string.Format(CultureInfo.CurrentCulture, Resources.PEG0008_UNEXPECTED_END_OF_INPUT, unexpected) }
