@namespace Pegasus.Parser
@classname PegParser
@using System.Linq;
@using Pegasus.Expressions;

grammar
  = <Grammar> __ settings:setting* initializer:initializer? rules:rule+ {
        return new Grammar(rules, settings, initializer.SingleOrDefault());
    }

setting
  = <KeyValuePair<string, string>> "@" key:identifier value:(string / dotted) semicolon? {
        return new KeyValuePair<string, string>(key, value);
    }

initializer
  = code:action semicolon? {
        return code;
    }

rule
  = <Rule> name:identifier displayName:string? equals expression:expression semicolon? {
        return new Rule(
            name: name,
            displayName: displayName.SingleOrDefault(),
            expression: expression);
    }

expression
  = <Expression> type:type? choice:choice {
        var typeValue = type.SingleOrDefault();
        return typeValue != null
            ? new TypedExpression(typeValue, choice)
            : choice;
    }

type
  = lt dotted:dotted gt {
        return dotted;
    }

choice
  = <Expression> head:sequence tail:(<Expression> slash sequence:sequence { return sequence; })* {
        if (tail.Count == 0)
        {
            return head;
        }
        else
        {
            return (Expression)new ChoiceExpression(new [] { head }.Concat(tail));
        }
    }

sequence
  = <Expression> elements:labeled* code:action {
        return (Expression)new SequenceExpression(elements.Concat(new Expression[] { new CodeExpression(code) }));
    }
  / elements:labeled* {
        if (elements.Count == 1)
        {
            return elements[0];
        }
        else
        {
            return new SequenceExpression(elements);
        }
    }

labeled
  = <Expression> label:identifier colon expression:prefixed {
        return (Expression)new PrefixedExpression(label, expression);
    }
  / prefixed

prefixed
  = <Expression> and expression:suffixed {
        return (Expression)new AndExpression(expression);
    }
  / not expression:suffixed {
        return (Expression)new NotExpression(expression);
    }
  / suffixed

suffixed
  = <Expression> expression:primary question {
        return (Expression)new RepetitionExpression(expression, min: 0, max: 1);
    }
  / expression:primary star {
        return (Expression)new RepetitionExpression(expression, min: 0, max: null);
    }
  / expression:primary plus {
        return (Expression)new RepetitionExpression(expression, min: 1, max: null);
    }
  / primary

primary
  = <Expression> name:identifier !(string? equals) {
        return (Expression)new NameExpression(name);
    }
  / literal
  / class
  / dot { return (Expression)new WildcardExpression(); }
  / lparen expression:expression rparen { return expression; }

/* "Lexical" elements */

action "action"
  = braced:braced __ { return braced.Substring(1, braced.Length - 2); }

braced
  = "{" parts:(braced / nonBraceCharacters)* "}"

nonBraceCharacters
  = chars:nonBraceCharacter+ { return string.Join("", chars); }

nonBraceCharacter
  = [^{}]

equals    = "=" __ { return "="; }
colon     = ":" __ { return ":"; }
semicolon = ";" __ { return ";"; }
slash     = "/" __ { return "/"; }
and       = "&" __ { return "&"; }
not       = "!" __ { return "!"; }
question  = "?" __ { return "?"; }
star      = "*" __ { return "*"; }
plus      = "+" __ { return "+"; }
lparen    = "(" __ { return "("; }
rparen    = ")" __ { return ")"; }
dot       = "." __ { return "."; }
lt        = "<" __ { return "."; }
gt        = ">" __ { return "."; }

/*
 * Modeled after ECMA-262, 5th ed., 7.6, but much simplified:
 *
 * * no Unicode escape sequences
 *
 * * "Unicode combining marks" and "Unicode connection punctuation" can't be
 *   part of the identifier
 *
 * * only [a-zA-Z] is considered a "Unicode letter"
 *
 * * only [0-9] is considered a "Unicode digit"
 *
 * The simplifications were made just to make the implementation little bit
 * easier, there is no "philosophical" reason behind them.
 */
identifier "identifier"
  = head:(letter / "_" / "$") tail:(letter / digit / "_" / "$")* __ {
        return head + string.Join("", tail);
    }

dotted "dotted identifier"
  = segment (dot segment)*

segment
  = identifier (lt dotted ("," __ dotted)* gt)?

/*
 * Modeled after ECMA-262, 5th ed., 7.8.4. (syntax & semantics, rules only
 * vaguely).
 */
literal "literal"
  = <Expression> value:(doubleQuotedString / singleQuotedString) flags:"i"? __ {
        return (Expression)new LiteralExpression(value, ignoreCase: flags.SingleOrDefault() == "i");
    }

string "string"
  = string:(doubleQuotedString / singleQuotedString) __ { return @string; }

doubleQuotedString
  = '"' chars:doubleQuotedCharacter* '"' { return string.Join("", chars); }

doubleQuotedCharacter
  = simpleDoubleQuotedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleDoubleQuotedCharacter
  = !('"' / "\\" / eolChar) char_:. { return char_; }

singleQuotedString
  = "'" chars:singleQuotedCharacter* "'" { return string.Join("", chars); }

singleQuotedCharacter
  = simpleSingleQuotedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleSingleQuotedCharacter
  = !("'" / "\\" / eolChar) char_:. { return char_; }

class "character class"
  = <Expression> "[" inverted:"^"? parts:(classCharacterRange / classCharacter)* "]" flags:"i"? __ {
        return (Expression)new ClassExpression(
            parts,
            negated: inverted.SingleOrDefault() == "^",
            ignoreCase: flags.SingleOrDefault() == "i");
    }

classCharacterRange
  = <CharacterRange> begin:bracketDelimitedCharacter "-" end:bracketDelimitedCharacter {
        return new CharacterRange(begin[0], end[0]);
    }

classCharacter
  = <CharacterRange> char_:bracketDelimitedCharacter {
        return new CharacterRange(char_[0], char_[0]);
    }

bracketDelimitedCharacter
  = simpleBracketDelimitedCharacter
  / simpleEscapeSequence
  / zeroEscapeSequence
  / hexEscapeSequence
  / unicodeEscapeSequence
  / eolEscapeSequence

simpleBracketDelimitedCharacter
  = !("]" / "\\" / eolChar) char_:. { return char_; }

simpleEscapeSequence
  = "\\" !(digit / "x" / "u" / eolChar) char_:. {
        return char_
            .Replace("b", "\b")
            .Replace("f", "\f")
            .Replace("n", "\n")
            .Replace("r", "\r")
            .Replace("t", "\t")
            .Replace("v", "\v");
    }

zeroEscapeSequence
  = "\\0" !digit { return "\x00"; }

hexEscapeSequence
  = "\\x" h1:hexDigit h2:hexDigit {
        return ((char)Convert.ToInt32(h1 + h2, 16)).ToString();
    }

unicodeEscapeSequence
  = "\\u" h1:hexDigit h2:hexDigit h3:hexDigit h4:hexDigit {
        return ((char)Convert.ToInt32(h1 + h2 + h3 + h4, 16)).ToString();
    }

eolEscapeSequence
  = "\\" eol:eol { return eol; }

digit
  = [0-9]

hexDigit
  = [0-9a-fA-F]

letter
  = lowerCaseLetter
  / upperCaseLetter

lowerCaseLetter
  = [a-z]

upperCaseLetter
  = [A-Z]

__ = (whitespace / eol / comment)*

/* Modeled after ECMA-262, 5th ed., 7.4. */
comment "comment"
  = singleLineComment
  / multiLineComment

singleLineComment
  = "//" (!eolChar .)*

multiLineComment
  = "/*" (!"*/" .)* "*/"

/* Modeled after ECMA-262, 5th ed., 7.3. */
eol "end of line"
  = "\n"
  / "\r\n"
  / "\r"
  / "\u2028"
  / "\u2029"

eolChar
  = [\n\r\u2028\u2029]

/* Modeled after ECMA-262, 5th ed., 7.2. */
whitespace "whitespace"
  = [ \t\v\f\u00A0\uFEFF\u1680\u180E\u2000-\u200A\u202F\u205F\u3000]